plugins {
    id 'com.android.application'
    id 'com.google.protobuf' version '0.8.18'
}

android {
    namespace 'com.example.fsandroid'
    compileSdk 32

    defaultConfig {
        applicationId "com.example.fsandroid"
        minSdk 26
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments "-DMNN_USE_LOGCAT=ON"
                cppFlags '-std=c++11'
                abiFilters 'arm64-v8a', 'x86_64'
            }
        }
        ndk {
            // Specifies the ABI configurations of your native
            // libraries Gradle should build and package with your app.
            abiFilters 'arm64-v8a'
        }
    }

    // For grpc-netty package
    packagingOptions {
        exclude 'META-INF/*'
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    buildTypes {
        debug {
            ndk {
                abiFilters 'arm64-v8a', 'x86_64'
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.18.1'
        }
    }
    buildFeatures {
        viewBinding true
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.21.7'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.49.1'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc { option 'lite' }
            }
        }
    }
}

dependencies {
    implementation 'io.grpc:grpc-netty:1.49.1'
    implementation 'io.netty:netty-all:4.1.84.Final'
    // grpc
    implementation 'io.grpc:grpc-android:1.49.1'
    implementation 'io.grpc:grpc-okhttp:1.49.1'
    implementation 'io.grpc:grpc-protobuf-lite:1.49.1'
    implementation 'io.grpc:grpc-stub:1.49.1'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'

    implementation fileTree(includes: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation 'org.yaml:snakeyaml:1.33'
}